;;; silstrup.dai -- Silstrup VAP area -- data from GEUS.

;; Chemistry

(defchemistry Silstrup multi
  (combine colloids Bromide Dimethoate Fenpropimorph Glyphosate Metamitron))

;; Soil 

(defhorizon Silstrup_Ap FAO3
  (clay 22.45 [%]) (silt 27.7 [%])(sand 49.85 [%])
  (humus (C2OM (C 1.798 [%]))) (dry_bulk_density 1.48 [g/cm^3])
  (hydraulic M_vG
             (Theta_sat 44 [%]) (Theta_res 0 [%])
             (n 1.1 []) (K_sat 306 [mm/h])
             (l 0.5 []) (alpha 0.01 [cm^-1])))

(defhorizon Silstrup_B FAO3
  (clay 27.97 [%]) (silt 26.17 [%]) (sand 45.86 [%])
  (humus (C2OM (C 0.47 [%]))) (dry_bulk_density 1.61 [g/cm^3])
  (hydraulic M_vG
             (Theta_sat 39 [%]) (Theta_res 0 [%])
             (n 1.13 []) (K_sat 36 [mm/h])
             (l 0.5 []) (alpha 0.012 [cm^-1])))

(defhorizon Silstrup_C FAO3
  (clay 26.28 [%]) (silt 20.86 [%]) (sand 52.86 [%])
  (humus (C2OM (C 0.116 [%]))) (dry_bulk_density 1.75 [g/cm^3])
  (hydraulic M_vG
             (Theta_sat 34 [%]) (Theta_res 0 [%])
             (n 1.1 []) (K_sat 19 [mm/h])
             (l 0.5 []) (alpha 0.012 [cm^-1])))

(defcolumn Silstrup default
  ;; 1D
  ;; (Movement original 
  ;;           (Tertiary pipes
  ;;                     (L 18 [m])
  ;;                     (pipe_position -110.0 [cm])))
  ;; 2D
  (Movement Rorrende
            (Geometry (zplus -2 -5 -10 -20 -25 -31
                             -35 -39
                             -50 -75 -100 -113 
                             -120 -180 -220 -300 -400 -500 [cm])
                      (xplus 10 25 50 100 150 200 300 400 600 900 [cm])))
  (Groundwater file "dk-silstrup.gwt"
               ;; GWT is measured at top of terrain.
               ;; Offset is calibrated based on drainage measurments.
               (offset 20 [cm]))
  (Chemistry Silstrup)
  (OrganicMatter none)
  (Soil (horizons ( -31.00 Silstrup_Ap)
                  ( -39.00 "Rorrende Bplow layer")
                  (-113.00 Silstrup_B)
                  (-500.00 Silstrup_C))
        (MaxRootingDepth 40 [cm])))

;; Management.

(defaction Silstrup activity
  (wait (at 1999 11 01))
    (stubble_cultivation -7 [cm])
  (wait (at 2000 04 19))
    ;; Cattle slurry 36.5 t/ha -  150 total-N, 36 P and 162 K,  kg/ha
    (fertilize (cattle_slurry (weight 36.5 [Mg w.w./ha])))
    (plowing (depth -22 [cm]))
  (wait (at 2000 05 01))
    (disk_harrowing -2 [cm])
    (disk_harrowing -2 [cm])
  (wait (at 2000 05 03))
    (seed_bed_preparation -5 [cm])
  (wait (at 2000 05 04))	
    ;; Sowing fodderbeat cultivare Kyros. depth 3 cm 
    ;; rowdistance 50 cm plantdistance 19 cm. 
    ;; seeding rate 3.0 kg/ha. Final plantnumber 5.7/m2			
    (sow "Fodder Beet")
  (wait (at 2000 05 15))	
    ;; Fertilization -  103 N, 26 P, 78 K, kg/ha			
    (fertilize (NPK01 (weight 103 [kg/ha])))
  (wait (at 2000 05 19)) 
    ;; Emergence BBCH stage 9 ( between 12/5 and 25/5)			
  (wait (at 2000 05 22))
    ;; BBCH stage 11			
    ;; Goltix WG+Betanal Optima 
    ;; (metamitron + phenmedipham + desmedipham + ethofumesat) - weeds 
    ;; 1.0 l + 1.0 l/ha			
    ;; Goltix WG: 700  g/kg  metamitron
    (spray Metamitron 700 [g/ha]) ;  (assume 1 kg = 1 l)
    ;; Potassium bromide molar mass 119.002 g/mol
    ;; Bromine atomic weight 79.904 g/mol
    ;; 30 kg potassium bromide = 30 * (79.904 / 119.002) = 20.14 kg bromine
    (spray Bromide 20.14 [kg/ha])
  (wait (at 2000 06 15))
    ;; BBCH stage 11-15			
    ;; Goltix WG+Betanal Optima 
    ;; (metamitron + phenmedipham + desmedipham + ethofumesat) - weeds
    ;; 1.0 l + 1.0 l/ha
    ;; Goltix WG: 700  g/kg  metamitron
    (spray Metamitron 700 [g/ha]) ;  (assume 1 kg = 1 l)
  (wait (at 2000 06 28))
    ;; BBCH stage 19
    ;; Fusilade X-tra (fluazifop-P-butyl) - weeds - 1.5 l/ha
    ;; BBCH stage 31
    ;; Biomass. Root + top:  213 g/10 m row - 100% DM
    ;; Pirimor G (pirimicarb) - pests - 0.3 kg/ha
  (wait (at 2000 07 11))
    ;; BBCH stage 33
  (wait (at 2000 07 12))
    ;; Goltix WG+Betanal Optima 
    ;; (B231 + phenmedipham + desmedipham + ethofumesat) - weeds 
    ;; 1.0 l + 1.0 l/ha
    ;; Goltix WG: 700  g/kg  metamitron		
    (spray Metamitron 700 [g/ha])       ;  (assume 1 kg = 1 l)
  (wait (at 2000 11 15))
    ;; Yield of root 13.45 t/ha 100% DM. Yield of top: 2.63 t/ha 100% DM
    (harvest "Fodder Beet")
  (wait (at 2001 04 01))
    (plowing (depth -18 [cm]))
  (wait (at 2001 05 07))
    (seed_bed_preparation -3 [cm])
  (wait (at 2001 05 08))
    ;; Fertilization 91 N, 13 P, 34 K, kg/ha
    (fertilize (NPK01 (weight 91 [kg/ha])))
    (seed_bed_preparation -6.5 [cm])
  (wait (at 2001 05 09))	
    (seed_bed_preparation -4.0 [cm])
    ;; Sowing spring barley Cultivare Otira. depth 3.5 cm 
    ;; rowdistance 12 cm seeding rate 155 kg/ha. Final plantnumber 298/m2
    (sow "Spring Barley")
  (wait (at 2001 05 20))
    ;; Emergence-  BBCH stage 9 (between 17/5 and 23/5)
  (wait (at 2001 05 22))
    ;; BBCH stage 10			
    ;; Fertilization 27 N, 4 P, 10 K, kg/ha
    (fertilize (NPK01 (weight 27 [kg/ha])))
  ;; 28-05-01 00:00	BBCH stage 12			
  ;; 09-06-01 00:00	Express (tribenuron-methyl) - weeds - 2 tablets/ha
  ;; 11-06-01 00:00	BBCH stage 24
  ;; 11-06-01 00:00	Biomass  34.7 g/m2 - 100% DM
  ;; 19-06-01 00:00	BBCH stage 31
  (wait (at 2001 6 21))
    ;; BBCH stage 32
    ;; Barnon Plus 3 - (flamprop-M-isopropyl) - weeds- 3.0 l/ha
    ;; Tilt Top (propiconazol + fenpropimorph) - fungi - 0.5 l pr. ha
    ;; Tilt Top: 375  g/l  fenpropimorph
    (spray Fenpropimorph 187.5 [g/ha])
  ;; 02-07-01 00:00	Biomass  51.1 g/m2 - 100% DM
  (wait (at 2001 07 04))
    ;; Tilt Top (propiconazol + fenpropimorph) - fungi - 0.5 l pr. ha
    ;; Tilt Top: 375  g/l  fenpropimorph
    (spray Fenpropimorph 187.5 [g/ha])
  (wait (at 2001 07 16))
    ;; Perfektion 500 (dimethoat) - pests - 0.6 l/ha
    ;; Perfekthion 500 S: 500  g/l  dimethoat
    (spray Dimethoate 300 [g/ha])
  (wait (at 2001 09 05))
    ;; Harvest of spring barley. ;; Stubbleheight 12 cm, 
    ;; grain yield 74.77 hkg/ha 85% DM, straw yield 28.62 hkg/ha 100% DM
    (harvest "Spring Barley" (stub 12 [cm]))
  (wait (at 2001 10 25))
     ;; Roundup Bio (glyphosate) - weeds - 4.0 l/ha			
     ;; 360 g/l * 4.0 l/ha = 1440 g/ha
    (spray Glyphosate 1440 [g/ha])
  (wait (at 2001 12 18))
    (plowing (depth -22 [cm]))
  (wait (at 2002 04 03))
    (seed_bed_preparation -3 [cm])
  (wait (at 2002 04 17))	
    (seed_bed_preparation -5 [cm])
  (wait (at 2002 04 22))
    ;; Cattle slurry application 40.3 t/ha
    ;; 194.0 total-N, 93.1 NH4-N, 42.9 P, 194.8 K, 1696.7 C, kg/ha
    (fertilize ("cattle_slurry" (weight 40.3 [Mg w.w./ha])))
    (seed_bed_preparation -7.5 [cm])
  (wait (at 2002 04 23))	
    ;; Fertilization 46.5 N, kg/ha
    (fertilize (AmmoniumNitrate (weight 46.5 [kg/ha])))
  (wait (at 2002 04 25))	
    (seed_bed_preparation -8 [cm])
    ;; Sowing maiz cv. Loft, depth 6 cm, rowdistance 75 cm, 
    ;; seeddistance 17 cm seeding rate 7.9 m2. final plantnumber 7.4 m2
    (sow Maize)
    ;; Fertilization 23.4 N, 13.1 P, kg/ha			
    (fertilize (NP (weight 23.4 [kg/ha]))))
         
;; Output

(deflog "Silstrup Content" "VAP Content"
  (where "log/Silstrup-content.dlf")
  (when daily)
  (entries &old 
           (content (z -3.5 [m])
                    (path column "${column}" Chemistry "*" 
                          trace "Metamitron" C)
                    (tag Metamitron)
                    (dimension "mg/L")
                    (spec chemical default C))
           (content (z -0.60 [m])
                    (path column "${column}" SoilWater Theta)
                    (tag "SW060cm")
                    (dimension "%")
                    (spec fixed SoilWater Theta))
           (content (z -1.10 [m])
                    (path column "${column}" SoilWater Theta)
                    (tag "SW110cm")
                    (dimension "%")
                    (spec fixed SoilWater Theta))
           ))

;; Program

(defprogram Silstrup VAP
  "Simulation for the Silstrup PLAP station."
  (station "Silstrup")
  (column Silstrup)
  (manager Silstrup)
  (weather default "dk-silstrup-hourly.dwf"
           (PrecipScale 1.41 1.42 1.35 1.24 1.13 1.11
                        1.10 1.10 1.11 1.14 1.23 1.37 []))
  
  (time 1999 9 1)
  (stop 2002 3 10)

  (output &old
          ("Field chemical" 
           (chemical "Metamitron-base")
           (where "log/${station}-field-Metamitron.dlf"))
          ("Field chemical"  (when weekly)
           (chemical "Metamitron-base")
           (where "log/${station}-field-Metamitron-weekly.dlf"))
          ("Silstrup Content")))

;; Plots

(defxysource "Retension Ap Silstrup GEUS" "Retension GEUS" 
  (y (convert (soil_Theta (horizon "Silstrup_Ap")
                          (top_soil true)
                          (h x))
              [])))

(defxysource "Retension Ap Silstrup HYPRES" "Retension HYPRES"
  (y (convert (soil_Theta (horizon ("Silstrup_Ap" (hydraulic hypres)))
                          (top_soil true)
                          (h x))
              [])))

(defxysource "Conductivity Ap Silstrup GEUS" "Conductivity GEUS" 
  (y (convert (soil_K (horizon "Silstrup_Ap")
                      (top_soil true)
                      (h x))
              [cm/d])))

(defxysource "Conductivity Ap Silstrup HYPRES" "Conductivity HYPRES"
  (y (convert (soil_K (horizon ("Silstrup_Ap" (hydraulic hypres)))
                      (top_soil true)
                      (h x))
              [cm/d])))

(defgnuplot "Hydraulic Ap Silstrup" "Hydraulic All"
  (hor "Ap")
  (source "Retension Ap Silstrup HYPRES"
          "Retension Ap Silstrup GEUS"
          "Conductivity Ap Silstrup HYPRES"
          "Conductivity Ap Silstrup GEUS"))

(defxysource "Retension B Silstrup GEUS" "Retension GEUS" 
  (y (convert (soil_Theta (horizon "Silstrup_B")
                          (top_soil false)
                          (h x))
              [])))

(defxysource "Retension B Silstrup HYPRES" "Retension HYPRES"
  (y (convert (soil_Theta (horizon ("Silstrup_B" (hydraulic hypres)))
                          (top_soil false)
                          (h x))
              [])))

(defxysource "Conductivity B Silstrup GEUS" "Conductivity GEUS" 
  (y (convert (soil_K (horizon "Silstrup_B")
                      (top_soil false)
                      (h x))
              [cm/d])))

(defxysource "Conductivity B Silstrup HYPRES" "Conductivity HYPRES"
  (y (convert (soil_K (horizon ("Silstrup_B" (hydraulic hypres)))
                      (top_soil false)
                      (h x))
              [cm/d])))

(defgnuplot "Hydraulic B Silstrup" "Hydraulic All"
  (hor "B")
  (source "Retension B Silstrup HYPRES"
          "Retension B Silstrup GEUS"
          "Conductivity B Silstrup HYPRES"
          "Conductivity B Silstrup GEUS"))

(defxysource "Retension C Silstrup GEUS" "Retension GEUS" 
  (y (convert (soil_Theta (horizon "Silstrup_C")
                          (top_soil false)
                          (h x))
              [])))

(defxysource "Retension C Silstrup HYPRES" "Retension HYPRES"
  (y (convert (soil_Theta (horizon ("Silstrup_C" (hydraulic hypres)))
                          (top_soil false)
                          (h x))
              [])))

(defxysource "Conductivity C Silstrup GEUS" "Conductivity GEUS" 
  (y (convert (soil_K (horizon "Silstrup_C")
                      (top_soil false)
                      (h x))
              [cm/d])))

(defxysource "Conductivity C Silstrup HYPRES" "Conductivity HYPRES"
  (y (convert (soil_K (horizon ("Silstrup_C" (hydraulic hypres)))
                      (top_soil false)
                      (h x))
              [cm/d])))

(defgnuplot "Hydraulic C Silstrup" "Hydraulic All"
  (hor "C")
  (source "Retension C Silstrup HYPRES"
          "Retension C Silstrup GEUS"
          "Conductivity C Silstrup HYPRES"
          "Conductivity C Silstrup GEUS"))

(defgnuplot sil-drainmass drainmass (area 1.69 [ha]))
(defgnuplot sil-drainmass-acc drainmass-acc (area 1.69 [ha]))

(defgnuplot Silstrup Station
  (station Silstrup)
  (graph &old 
         "Hydraulic Ap Silstrup"
         "Hydraulic B Silstrup"
         "Hydraulic C Silstrup"
         (horizontal (source &old 
                             (column (file "log/${station}-content.dlf")
                                     (tag "Metamitron"))))
         (theta (depth "SW060cm"))
         (theta (depth "SW110cm"))
         ;; Metamitron and Bromide first applied 2000-05-22.
         (sil-drainmass (begin 2000 05 01) (chemical Metamitron))
         (sil-drainmass-acc (begin 2000 05 01) (chemical Metamitron))
         (sil-drainmass (begin 2000 05 01) (chemical Bromide))
         (sil-drainmass-acc (begin 2000 05 01) (chemical Bromide))
         ;; Dimethoate first applied 2001-07-16
         ;; Fenpropimorph first applied 2001-06-21
         ;; Glyphosate first applied 2001-10-25
         (sil-drainmass (begin 2001 06 01) (chemical Dimethoate))
         (sil-drainmass (begin 2001 06 01) (chemical Fenpropimorph))
         (sil-drainmass (begin 2001 06 01) (chemical Glyphosate))
         (sil-drainmass-acc (begin 2001 06 01)  (chemical Dimethoate))
         (sil-drainmass-acc (begin 2001 06 01) (chemical Fenpropimorph))
         (sil-drainmass-acc (begin 2001 06 01) (chemical Glyphosate))
         ))

;;; silstrup.dai ends here.
