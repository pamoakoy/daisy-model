;;; afforestation.dai -- Sample setup using afforestation.

(input file "dk-soil.dai")

(defvegetation Oak afforestation
  "A growing oak forest."
  ;; Date forest was planted.
  (planting_time 1970 5 1)
  ;; Forest height as a function of years since planting.
  (canopy_height (0 [y] 0 [m]) (1 [y] 1 [m]) (2 [y] 1.5 [m]) (5 [y] 2 [m])
                 (10 [y] 3 [m]) (20 [y] 4 [m]) (30 [y] 5 [m]))
  ;; Forest height as a function of years since planting.
  (root_depth (0 [y] 0.3 [m]) (1 [y] 1.0 [m]) (2[y]  1.5 [m]))
  ;; LAI shape during a year, 1 representing the relative minimum LAI
  ;; and 5 the maximum LAI, to be scaled for yearly growth according
  ;; to LAI_min and LAI_max.
  (LAI_shape (1 [d] 1 []) (135 [d] 1 []) (160 [d] 5.0 []) 
             (305 [d] 5.0 []) (320 [d] 1 []) (365 [d] 1 [])) 
  ;; Minimum and maximum LAI as a function of year after planting.
  (LAI_min (0 [y] 0.1) (1 [y] 0.2) (5 [y] 0.3))
  (LAI_max (0 [y] 0.5) (1 [y] 1) (5 [y] 3) (10 [y] 4) (20 [y] 5))

  ;; The nitrogen demand of the forest has two components, a short
  ;; time dynamic which depends on LAI, and a long time accumulation
  ;; of nitrogen.
  (N_nonleaves (0 [y] 0 [kg N/ha]) (1 [y] 10 [kg N/ha]) 
               (10 [y] 100 [kg N/ha]) (30 [y] 200 [kg N/ha]))
  ;; Multiply with LAI to get short time dynamic component.
  (N_per_LAI 10 [kg N/ha])
  
  ;; Litterfall during a year, 0 representing no litterfall, and 5
  ;; representing the highest litterfall.
  (litterfall_shape (300 [d] 0 []) (305 [d] 5 []) (320 [d] 0 [])) 
  ;; Total litterfall as a function of years after planting.
  (litterfall_total (0 [y] 0.5 [Mg DM/ha]) (1 [y] 1 [Mg DM/ha])
                    (5 [y] 3 [Mg DM/ha]) (10 [y] 4 [Mg DM/ha])
                    (20 [y] 5 [Mg DM/ha]))
  ;; Content of litterfall.
  (litterfall_C_per_DM 0.5 [])
  (litterfall_C_per_N 50 [])

  ;; Rhizodeposition during a year, 0 representing no rhizodeposition, and 5
  ;; representing the highest rhizodeposition.
  (rhizodeposition_shape (300 [d] 0) (305 [d] 5) (320 [d] 0)) 
  ;; Total rhizodeposition as a function of years after planting.
  (rhizodeposition_total (0 [y] 0.5 [Mg C/ha]) (1 [y] 1 [Mg C/ha])
                    (5 [y] 3 [Mg C/ha]) (10 [y] 4 [Mg C/ha])
                    (20 [y] 5 [Mg C/ha]))
  ;; Content of rhizodeposition.
  (rhizodeposition_C_per_N 50 [])

  ;; Constant parameters inherited from the 'permanent' model.
  (LAIfactor 0.5 [])	
  (Canopy (IntcpCap 0.3 [mm])
          (rs_min 70.0)
          (EpFac 1.136 [])))

(defcolumn Oak Askov
  (Vegetation Oak)
  ;; Change in pH with depth, as a function of calendar year.
  (Soil (pH_depth_year (1970 (  0 [cm] 7.0 [pH]) (-100 [cm] 7.0 [pH]))
                       (1980 (  0 [cm] 6.5 [pH]) ( -10 [cm] 6.5 [pH])
                             (-20 [cm] 7.0 [pH]) (-100 [cm] 7.0 [pH]))
                       (2000 (  0 [cm] 6.0 [pH]) ( -10 [cm] 6.0 [pH])
                             (-20 [cm] 6.8 [pH]) (-100 [cm] 7.0 [pH]))))
  ;; Choose a deposition model that depends on vegetation.
  (Bioclimate original 
              (Deposition Gundersen_et_al))
  (OrganicMatter none)
  (Chemistry none)
  (Groundwater deep))

(defprogram Oak Daisy
  (weather default "dk-vestskoven.dwf")
  (column Oak)
  (time 2000 12 1 23)
  (stop 2002 12 31 0)
  (manager activity)
  (output))

(run Oak)

;;; afforestation.dai ends here.
